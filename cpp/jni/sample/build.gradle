plugins {
    id 'java'
    id 'java-library'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

compileJava {
    options.compilerArgs += ["-h", file("src/main/include")]
}

task compileJNI {
    dependsOn compileJava

    doLast {
        exec {
            commandLine 'sh', '-c', 'mkdir -p natives && cd natives && cmake .. && make'
        }
    }
}

clean.doFirst {
  delete fileTree('src/main/include') {
    include 'jni_*.h'
  }
}

processResources {
  dependsOn compileJNI
}

test {
    useJUnitPlatform()
}

tasks.withType(Test) {
    systemProperty "java.library.path", "natives"
    testLogging.showStandardStreams = false
}
